-- given a list of names generate
-- and print teams of three

load system util.
load system io.
load system dataframe.

-- team participants
let df = dataframe @dataframe().
df @read_csv("names.csv",true).
let names = df @col(0).

-- shuffle names
names @shuffle().

-- select teams of three
let teams = [].
repeat do
  if len(names) < 3 do
    let teams = teams + [names].
    break.
  elif names is [m1|m2|m3|names] do
    let teams = teams + [[m1,m2,m3]].
  end
until names is [].

-- print teams
for (i,team) in util @zip(1 to len(teams),teams) do
  let team_str = "team " + i + (":  " if i < 10 else ": ").
  repeat do
    let [name|team] = team.
    let team_str = team_str + name + ("" if team is [] else ", ").
  until team is [].
  io @println team_str.
end
