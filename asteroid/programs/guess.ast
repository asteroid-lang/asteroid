-- guess that number game from http://inventwithpython.com/invent4thed/
-- written by Lutz Hamel, (c) University of Rhode Island

load system io.
load system util.
load system random.
load system type.

random @seed 42. -- make it predictable

io @println "".
io @println "     \||||||||||||||||||/ ".
io @println "    - Guess that Number - ".
io @println "     /||||||||||||||||||\ ".
io @println "".


let name = io @input "Hello! What is your name? ".
let guesses_made = 0.
let number = random @randint (1, 20).

io @println (name + ", I am thinking of a number between 1 and 20, can you guess it?").

while guesses_made < 6 do
    let guess = type @tointeger (io @input "Take a guess: ").
    let guesses_made = guesses_made + 1.

    if guess < number do
        io @println "Your guess is too low."
    elif guess > number do
        io @println "Your guess is too high."
    elif guess == number do
        break.
    end
end

if guess == number do
    io @println ("Good job, " + name + "! You guessed my number in " + guesses_made + " guesses!").
else do
    io @println ("Nope. The number I was thinking of was " + number).
end

io @println "".
