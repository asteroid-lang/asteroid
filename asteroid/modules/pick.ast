------------------------------------------------------------------
-- pick.ast
--
-- this module implements the Pick structure
--
-- (c) Lutz Hamel, University of Rhode Island
------------------------------------------------------------------

load system random.

------------------------------------------------------------------
structure __PICK__
------------------------------------------------------------------
-- A pick object holds a list of items that
-- we can randomly pick from with the 'pickitems'
-- member function

  with

  data items.

  function pickitems
    with none do
        let ix = random @randint(0, this @items @length()-1).
        return this @items @ix.
    with n:%integer do
        let l = [].
        for i in 1 to n do
            let ix = random @randint(0, this @items @length()-1).
            l @append(this @items @ix).
        end
        return l.
  end

  --make sure that we only pass lists into the constructor
  function __init__ with l:%list do
      let this @items = l.
  end

end -- __PICK__

------------------------------------------------------------------
structure __PICK_MODULE__
------------------------------------------------------------------
with

function pick with l:%list do
   return __PICK__(l).
end

end -- __PICK_MODULE__

------------------------------------------------------------------
let pick = __PICK_MODULE__().
