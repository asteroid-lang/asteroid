------------------------------------------------------------------
-- stream.ast
--
-- this module implements a Stream structure that allows the
-- user to turn any list into a stream with supporting operations
-- like 'peek', 'get', and 'eof'.
--
-- (c) University of Rhode Island
------------------------------------------------------------------

load system util.

------------------------------------------------------------------
structure __STREAM__
------------------------------------------------------------------
-- Asteroid stream implementation based on lists
with

data stream.
data curr_ix.

function __init__
  with stream:%list do
    let this @stream = stream @copy(). -- shallow copy of input list
    let this @curr_ix = 0.
  end

function append
  with item do
    this @stream @append item.
  end

function eof
  with none do
    if this @curr_ix == this @stream @length() do
      return true.
    else do
      return false.
    end
  end

function get
  with none do
    if this @eof() do
      return none.
    else do
      let v = this @stream @(this @curr_ix).
      let this @curr_ix = this @curr_ix + 1.
      return v.
    end
  end

function map
  with f:%function do
    for i in range(this @stream @length()) do
      let this @stream @i = f(this @stream @i).
    end
  end

function peek
  with none do
    if this @eof() do
      return none.
    else do
      return this @stream @(this @curr_ix).
    end
  end

function rewind
  with none do
    let this @curr_ix = 0.
  end

function __string__
with none do return
escape
"
from asteroid.state import state
global __retval__

this_val = state.symbol_table.lookup_sym('this')

# destructure object
(OBJECT,
 (STRUCT_ID, (ID, struct_id)),
 (OBJECT_MEMORY, (LIST, memory))) = this_val

__retval__ = ('string', term2string(memory[0]))
"
end

end -- __STREAM__


------------------------------------------------------------------
structure __STREAM_MODULE__
------------------------------------------------------------------
with

function stream with l:%list do
   return __STREAM__(l).
end

end -- __STREAM_MODULE__

------------------------------------------------------------------
let stream = __STREAM_MODULE__().
